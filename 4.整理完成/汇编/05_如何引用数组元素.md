# 1. 段地址:偏移地址

```
编程的时候只能使用逻辑地址，不能使用物理地址。
段起始地址的16进制个位必须=0, 否则不能成为段起始地址
段长度=2233F-12340+1=22340-12340=10000h=64K
段起始地址	12340h	00h	1234:0000
	12341h	11h	1234:0001
	……	22h	
	12350h	33h	段起始地址

     1234段	……	44h	
1235:0048	12398h	55h	 1234:0058
	……	66h	  1235段
    段结束地址	2233Fh	77h	 1234:FFFF
	……	88h	
	2234Fh	99h	段结束地址
一个物理地址可以表示成多个逻辑地址, 例如:
12398h=1234:0058=1235:0048=1236:0038
=1230:0098 
段地址的1相当于偏移地址的10h:
段地址+1就意味着偏移地址-10h;
段地址-1就意味着偏移地址+10h;
```

# 2. 直接寻址、间接寻址

```
设从地址1000h:2000h起存放以下4个字节:
1000:2000 12h
1000:2001 34h
1000:2002 56h
1000:2003 78h
现编程计算这4个字节之和并保存到寄存器AL中:
mov ax, 1000h
mov ds, ax;ds是数据段寄存器
		 ;除了cs不能通过mov修改外，ds、es、ss均可以修改。
mov al, 0
mov bx, 2000h
mov cx, 4
next:
add al, ds:[bx]
add bx, 1
sub cx, 1
jnz next
```

假定要取出1000:2000指向的字节并保存到AL中, 则有两种方法:

(1) 直接寻址(用常数来表示变量的偏移地址)

`mov al, 1000h:[2000h]; `<font color='red'>语法错误: 段地址必须用段寄存器来表示, 不能用常数</font>
`mov al, ds:[2000h];` ds:2000h可以看作一个指针
								;   ds:[2000h]表示上述指针指向的对象
`mov al, byte ptr ds:[2000h];` byte ptr表示对象的类型是byte
												;   ptr是pointer的缩写
可以把上述语句转化成C语言语法来写:

```
typedef unsigned char byte;
al = *(byte *)(ds:2000h);
```

(2)间接寻址(用寄存器、寄存器+常数来表示变量的偏移地址)

```
mov bx, 2000h
mov al, ds:[bx]; 用于间接寻址的寄存器仅限于:
                  ; bx, bp, si, di
mov al, byte ptr ds:[bx]
```

什么样的情况下, 必须使用byte ptr、word ptr、dword ptr这三种类型修饰?

```
mov ds:[bx], 1; 语法错误
mov byte ptr ds:[bx], 1; 正确①
mov word ptr ds:[bx], 1; 正确②
mov dword ptr ds:[bx], 1; 正确③
```

```
1000:2000 12h->01h  ->01h  	->01h
1000:2001 34h ①    	->00h  	->00h
1000:2002 56h         ②     ->00h
1000:2003 78h                ->00h
                               ③
```

# 3. 如何引用数组元素

引用数组元素的例子: http://10.71.45.100/bhh/addr.asm

```
data segment
abc db 1,2,3,4
xyz dw 789Ah, 0BCDEh
asd dd 12345678h, 56789ABCh; 首元素为asd[0]
                           ; 末元素为asd[4]
data ends

code segment
assume cs:code, ds:data
main:
   mov ax, data
   mov ds, ax
   ;形式(1)
   mov ah, abc[1]; \编译后变成:
   mov ah, [abc+1];/mov ah, ds:[1]; 直接地址
   ;形式(2)
   mov bx, offset abc
   mov ah, [bx+1]; 间接地址
   ;形式(3)
   mov bx, 1
   mov ah, abc[bx] ;\间接地址
   mov ah, [abc+bx];/
   ;形式(4)
   mov bx, offset abc
   mov si, 1
   mov ah, [bx+si]

   mov ax, xyz[2] ;\编译后变成:
   mov ax, [xyz+2];/mov ax, ds:[6]
   
   mov ah, abc; 等价于mov ah, abc[0]
   mov ah, [abc]; 效果与上面这句等价
                ;编译后变成mov ah, ds:[0]
   mov ax, xyz
   mov ax, [xyz]; 其完整形式是:
                ; mov ax, word ptr ds:[xyz]
                ; 编译后变成mov ax, ds:[4]
   
   mov ah, 4Ch
   int 21h
code ends
end main
```

```
mov ah, [abc]中, 
[abc]可以理解成地址abc所指向的对象。
mov ah, [abc]的完整形式其实是:
mov ah, byte ptr ds:[abc]
其中ds是变量abc的段地址
byte ptr表示地址abc所指的对象是一个字节。
若[]中不包含寄存器bp，则该变量默认的段地址一定是ds,故在源程序addr.asm中可以省略ds:。
byte ptr有点类似于C语言中的(char *), 其中ptr是单词pointer的缩写。
byte ptr ds:[abc]表示地址ds:abc所指的对象是一个byte。相当于C语言的如下描述:
*(char *)(ds:abc)
汇编语言的语句中，如果源操作数或目标操作数的其中之一有明确的类型即宽度，则另外一方不需要指定类型。
在本例中,由于ah是8位宽度，故可以省略源操作数的类型byte ptr。
```

```
如何在dosbox86中调试程序:
1. 先编译连接
2. debug->debug
3. view->cpu 查看机器语言
4. F8单步跟踪

如何在命令行中调试:
masm addr;
link addr;
td addr
其中td指Turbo Debugger。
微软对应的调试器叫CodeView，但功能不如td。

C语言中引用数组元素的例子: http://10.71.45.100/bhh/addr.c
编译调试addr.c的步骤:
复制addr.c到dosbox86\tc中
dosbox86->file->DOS shell
cd \tc
tc
按Alt+F选择菜单file->load->addr.c
按Alt+C选择菜单compile->build all
按Alt+F选择菜单file->quit
td addr
view->cpu
按F5放大窗口
F8单步调试
调试完成按file->quit
exit 返回dosbox86集成环境
```

输入一个字符串再输出的例子: http://10.71.45.100/bhh/io.asm

```
data segment
a db 100 dup(0)
data ends
code segment
assume cs:code, ds:data
main:
   mov ax, data
   mov ds, ax
   mov bx, 0
input_next:
   mov ah, 1
   int 21h; AL=getchar()
   cmp al, 0Dh; 判断是否回车符
   je input_done
   mov a[bx], al
   add bx, 1
   jmp input_next
input_done:
   mov a[bx], 0; 填充字符串结束标志
   mov ah, 2
   mov dl, 0Dh
   int 21h; 输出回车
   mov ah, 2
   mov dl, 0Ah
   int 21h; 输出换行
   ;
   mov bx, offset a
   mov si, 0
output_next:
   mov dl, [bx+si]
   cmp dl, 0
   je output_done
   mov ah, 2
   int 21h; 输出DL中的字符
   add si, 1
   jmp output_next
output_done:
   mov ah, 4Ch
   int 21h
code ends
end main
```



用汇编语言控制文本方式下整个屏幕的输出:
屏幕坐标示意图: http://10.71.45.100/bhh/screen.bmp
输出2000个'A'的例子: http://10.71.45.100/bhh/2000a.asm
屏幕上移动的'A'的例子: http://10.71.45.100/bhh/mova.asm
