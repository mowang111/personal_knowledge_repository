# 1. 实模式(real mode)与保护模式(protected mode)有什么区别?

## (1)实模式

段地址为16位,偏移地址为16位
物理地址=段地址*10h+偏移地址
最大可寻址的内存空间为1M
每个段的最大长度为64K即10000h
每个段的属性为可读、可写、可执行
中断向量表位于固定地址空间0:0~0:3FF, 每个中断
向量的宽度为4字节

## (2) 保护模式

段地址16位,偏移地址为16位或32位
物理地址=gdt[段地址].base_addr + 偏移地址
最大可寻址的内存空间为4G
每个段的最大长度为4G
每个段的属性可以设写为:可读+可执行、可读+可写
中断向量表的地址可以由用户设定,且每个中断向量
的宽度为8字节

# 2. gdt: global descriptor table全局描述符表

```
它是一个结构数组, 数组里的每个元素均为8字节
宽度的结构.

设ds=8, esi=10000h, 则ds:esi对应的物理地址=
12345678h+10000h=12355678h
ds对应的段长度=FFFFF+1=100000h字节
ds对应的段属性: 1001 0011; 0100

设gdt表的首地址为2000h
+2000 00, 00, 00, 00, 00, 00, 00, 00
+2008 FF, FF, 78, 56, 34, 93, 4F, 12
+2010 FF, FF, 78, 56, 34, 9B, 4F, 12
+2018 ...
gdt+8指向的8字节中第2、3、4、7这四字节构成
base_addr=12345678h;
第0、1及第6字节的低4位构成limit=FFFFFh
(limit是段内的最大偏移地址);
第5个字节及第6字节的高4位是段的属性:
其中第5字节展开成二进制 = 
1 0 0 1   0 0 1 1

- === -   -   - -
  | |   |   |   | |
  | |   |   |   | +---Accessed已访问过
  | |   |   |   +--0=代码段不可读或数据段不可写
  | |   |   |      1=代码段可读或数据段可写
  | |   |   +---0=数据段(可读),1=代码段(可执行)
  | |   +---S(1=代码或数据段, 0=TSS或gate)
  | +---DPL(descriptor privilege level)
  |     00=最高级别,11=最低级别
  +-----Present(1=存在, 0=不存在)
  第5字节第3位和第1位组合起来共有以下4种变化:
          1   1 = 可读可执行代码段
          1   0 = 只执行代码段(不可读)
          0   1 = 可读可写数据段
          0   0 = 只读数据段

第6个字节的高4位也是段的属性:
0  1  0  0

|      |      |      |
| ---- | ---- | ---- |
|      |      |      |

|  |  +--+---保留
|  +----D(1=32位的段, 0=16位的段)
+--Granularity(粒度)
   1=段长度单位为页(1页=4K), 0=单位为字节
当limit=FFFFFh,且G=1时, 段的长度=
(FFFFF+1) * 4K = 100000h * 1000h = 4G字节
```



# 3. 如何根据段地址查询段首地址?

gdt+(段地址 & FFF8)->8字节的描述符
从8字节的描述符中提取第2、3、4、7字节即为段首地址

# 4. 访问限制

```
某个正在运行的进程的权限用CPL(current privilegelevel, 位于CS的低2位)表示, 当CPL<=DPL时, 表示
该进程可以访问目标段.
cs    ip
000B:1000 jmp 10h:0; 跳成功的话, cs=10h, 其低2位=00
1011         假定gdt+10h指向的8字节中第5字节=9Bh  
  --         则目标段描述符的DPL为00
CPL=3        由于CPL > DPL, 故jmp不会成功
```

# 5.其他

## 1.如何区分段描述符与门描述符及系统描述符?
(1) 数据段、代码段、堆栈段描述符的access的S位(即Bit4)=1;
(2) 门描述符(call gate、interrupt gate、trap gate、task gate)的S位=0;
(3) 系统描述符TSS(task state segment)及LDT(local descriptor table)的S位=0;

## 2.如何区分数据段描述符与代码段描述符?
(1) 数据段描述符的access的Bit3=0, 表示不可执行;
(2) 代码段描述符的access的Bit3=1, 表示可执行;

##  3.数据段描述符与代码段描述符的access的Bit1有何区别?
(1) 数据段描述符access的Bit1=1时表示该数据段可写, 否则表示只读;
(2) 代码段描述符access的Bit1=1时表示该代码段可读, 否则表示只执行(不可读);

## 4.数据段描述符与代码段描述符的access的Bit2有何区别?
(1) 数据段描述符access的Bit2=1时表示该数据段为expanded-down类型, 否则为普通数据段。
 expanded-down类型适用于堆栈段, base=段起始地址+limit+1, limit跟普通数据段一样。
 普通数据段的base=段起始地址。
(2) 代码段描述符access的Bit2=1时表示该代码段为conforming类型, 否则为普通代码段。
 conforming类型的代码段是指当调用者调用conforming代码段内的函数并执行到该函
 数内的指令时，当前CPL仍旧等于调用者的CPL而不会变成conforming段的DPL。当调用
 者调用普通代码段(non-conform)内的函数并执行到该函数内的指令时，当前CPL会变
 成non-conforming段的DPL。

##  5.描述符的哪个位决定对应的段是32位还是16位?
gran中的Bit6=1时表示该段为32位，否则表示该段为16位。当数据段的limit+1超过64K时，
应该把Bit6置1。当代码段的指令工作在32位模式下时，必须把Bit6置1。请注意16位代码
段与32位代码段的区别不仅仅在于偏移地址宽度的差异，更在于同样的机器码在不同模式
下执行时会有不一样的结果，例如:
(1) 33 C0这组机器码在16位模式下表示xor ax,ax而
在32位模式下表示xor eax,eax
(2) lodsb指令在16位模式下表示AL=ds:[si]再si++，
而在32位模式下表示AL=ds:[esi]再
    esi++
(3) call far ptr指令在16位模式下要求目标地址
为"16位段地址:16偏移地址", 而在32位
    模式下, 要求目标地址为
    "16位段地址:32位偏移地址"

## 6.CPU如何判断当前进程能否访问某个段?
(1) CPL是当前进程CS中的低2位, 表示该进程的权限。DPL是段描述符(descriptor)中成员access的第6位及第5位, 表示该描述符所描述的段的访问权限。
(2) CPL <= DPL时，当前进程才可以访问该DPL对应的数据段
 0      3      当前进程访问gate时也使用此规则
(3) CPL >= DPL时，当前进程才可以通过调用门call该DPL对应的代码段
 3      0

## 7.当ring3进程通过call gate调用ring0函数时，ring0函数使用什么方法阻止自己访问
由调用者传递过来的参数所指向的调用者本来无权访问的内存块?

```
ring3:
push 18h; 段地址    \  18h:28h->内存块
push 28h; 偏移地址  /  其中ring3无权访问18h这个段(假定18h描述符的DPL=0)
;call 0020h:12345678h 不能直接call ring0段(假定20h描述符的DPL=0)
call far ptr 10h:00000000h; 其中10h是call gate的selector
here:
add esp, 8
;===============================================
;         0   1   2   3   4   5    6   7
;gdt+10h 78h,56h,20h,00h,02h,0ECh,34h,12h; 调用门
;                       参数 权限=
;                       个数 1110 1100(P=1, DPL=3, S=0, type=0Ch)
;目标函数的地址=0020h:12345678h
在cs:eip切换到0020h:12345678h前,
CPU从当前任务状态段tss(task state segment)中取出
ring0的堆栈指针ss0:esp0, 并设置ss=ss0, esp=esp0
tss里面包含了当前进程的所有寄存器值，还有切换到
ring0、ring1、ring2时要用到的堆栈指针ss0:esp0、 
ss1:esp1、ss2:esp2, 当前tss的首地址保存在寄存器tr中。
接下去在新堆栈中压入以下信息:
esp+00  here的偏移地址
esp+04  old cs; 注意old cs的低2位是调用者的CPL
esp+08  28h; 偏移地址
esp+0C  18h; 段地址
esp+10  old esp
esp+14  old ss
老堆栈中压入的信息如下(没有cs及返回地址):
esp+00  28h; 偏移地址
esp+04  18h; 段地址
注意新堆栈的权限是ring0，其内容不可能被调用者篡改。
到达目标函数后, CPL=0, 目标函数会在任何一个段寄存器
被赋值前对源操作数做权限修正, 例如:
ring0:
push ebp
mov ebp, esp
;指令执行到此, 堆栈结构如下:
;ebp+00  old ebp
;ebp+04  返回地址
;ebp+08  old cs; 注意old cs的低2位是调用者的CPL
;ebp+0C  28h; 偏移地址
;ebp+10  18h; 段地址
;ebp+14  old esp
;ebp+18  old ss
mov ax, [ebp+10h]; 18h=调用者传递过来的内存块段地址
;mov ds, ax      ; 这里不可以不经权限验证就赋值给ds
mov cx, [ebp+08h]; cx=old cs
arpl ax, cx; 当ax.RPL<cx.RPL时, ax.RPL = cx.RPL
           ; arpl: adjust requestor's privilege level
           ; ax=2Bh=0000 0000 0010 1011
           ;       =0000 0000 0010 1000 + 11
mov ds, ax ; 此时会引发GPF(General Protection Fault)
           ; 因为CPU会按以下规则做权限检查:
           ; max(CPL,RPL in ax) <= ax所指段的DPL
           ;          3                0
```



```
.386P
;=========================================================
;copyright (c) Black White
;email: iceman@zju.edu.cn
;------------Structure of Descriptor----------------------
desc struc
lim_0_15   dw 0; +0+1 lower 16 bits for segment limit(total 20 bits)
bas_0_15   dw 0; +2+3 lower 16 bits for segment's base address(total 32 bits)
bas_16_23  db 0; +4   16-23 bits for segment's base address
access     db 0; +5   access right byte; P(B7) DPL(B65) S(B4) T(B321) A(B0)
gran       db 0; +6   granularity byte; G(B7) D(B6) 0(B5) AVL(B4) lim_16_19(B3210)
bas_24_31  db 0; +7   higher 8 bits for segment's base address
desc ends

;--------------------------------------------------
;access:
;       P(B7)    Present bit(whether this segment exists)
;       DPL(B65) Descriptor Privilege Level bits(varies from 0 to 3)
;       S(B4)    Segment bit describing segment's attr:
;                1=data/code segment descriptor;
;                0=system descriptor(i.e. LDT and TSS desc.) and gate 
;                descriptor(i.e. task, call, interrupt, and trap gates' desc.)
;       T(B321)  Type bits:
;       1. if S==1(data/code/stack segment)
;          B3: Execute bit(1=code, 0=data or stack)
;          B2: 1=Expand-down bit for stack, 
;              1=Conform bit for code
;          B1: 1=Writable bit for data, 1=Readable bit for code
;          B0: Access bit, 1=accessed, 0=not accessed
;       2. if S==0(system desc. and gate desc.)
;          B3210: type for system desc. and gate desc.
;           00h: Undefined           08h: Undefined
;           01h: 286 TSS desc.       09h: 386 TSS desc.
;           02h: LDT desc.           0Ah: Undefined
;           03h: busy 286 TSS desc.  0Bh: busy 386 TSS desc.
;           04h: 286 call gate desc. 0Ch: 386 call gate desc.
;           05h: task gate desc.     0Dh: Undefined
;           06h: 286 intr gate desc. 0Eh: 386 intr gate desc.
;           07h: 286 trap gate desc. 0Fh: 386 trap gate desc.
;gran:
;     granularity byte.
;     G(B7)      Granularity bit: 1=page granularity, 0=byte granularity
;     D(B6)      Data attr bit: 1=32-bit addr, 0=16-bit addr
;     0(B5)      Reserved bit: should be kept 0
;     AVL(B4)    Available bit: kept for user
;     lim_16-19(B3210) higher 4 bits for segment limit
;--------------------------------------------------
;-----------End of Structure of descriptor---------

;----------Structure of gdtr-----------------------
_gdtr struc
_gdtr_lim       dw 0
_gdtr_bas_0_15  dw 0
_gdtr_bas_16_31 dw 0
_gdtr ends
;---------End of Structure of gdtr------------------------

stk segment stack use16
   dw 400h dup(0)
stk ends

data segment use16
gdt  label byte
null_desc desc <0000h, 0000h, 00h, 00h, 00h, 00h>
vram_sele equ $ - offset gdt; 08h
vram_desc desc <0FFFFh, 8000h, 0Ah, 93h, 4Fh, 00h> ; 32-bit
data_sele equ $ - offset gdt; 10h
data_desc desc <0FFFFh, 0000h, 00h, 93h, 00h, 00h> ; 16-bit
tran_sele equ $ - offset gdt; 18h
tran_desc desc <0FFFFh, 0000h, 00h, 9Bh, 00h, 00h> ; 16-bit
pseg_sele equ $ - offset gdt; 20h
pseg_desc desc <0FFFFh, 0000h, 00h, 9Bh, 40h, 00h> ; 32-bit
code_sele equ $ - offset gdt; 28h
code_desc desc <0FFFFh, 0000h, 00h, 9Bh, 00h, 00h> ; 16-bit
gdt_len equ $ - offset gdt
gdtr _gdtr <0, 0, 0>
succ_msg db 'Success!', 0Dh, 0Ah, '$'
fail_msg db 'Failure!', 0Dh, 0Ah, '$'
old_a20_status db 0
data_len equ $ - offset gdt
data ends


pseg segment use32;use32表示偏移地址为32位，只能在保护模式下使用
assume cs:pseg
protect:
   cld
   mov ax, vram_sele; offset vram_desc
   mov ds, ax; AX中的低2位称为RPL
   mov es, ax; 也会与vram_sele指向的描述符的DPL
             ; 比较
   mov ecx, 80*25
   mov esi, 10000h
   mov edi, esi
reverse_color:
   lodsw; AX=DS:[ESI], ESI+=2
   ror ah, 4
   stosw
   loop reverse_color
   db 0EAh; 48-bit jmp far ptr instruction
   dd offset transit; 32-bit offset
   dw tran_sele     ; 16-bit selector
                    ; jmp tran_sele:transit
pseg_len equ $ - offset protect
pseg ends

tran_seg segment use16
assume cs:tran_seg
transit:
   mov ax, data_sele
   mov ds, ax
   mov dx, data
   mov ebx, 10000h-1; limit must be compatible with real mode segment
   mov al, 93h
   mov ah, 00h
   mov si, data_sele
   db 9Ah ; call far ptr code_sele:fill_gdt_item
   dw offset fill_gdt_item
   dw code_sele
   ;
   mov ax, data_sele
   mov ds, ax; refresh segment descriptor cache in protected mode
   mov es, ax; so that DS & ES's limit and access will be compatible with 
             ; those in real mode
   ;
   mov eax, cr0
   and eax, 7FFFFFFEh; remove paging & protecting flag   
   mov cr0, eax; disable protected mode
   db  0EAh    ; Now return to real mode! Use real mode address!
   dw  offset back_real
   dw  code
tran_seg_len equ $ - offset transit
tran_seg ends

code segment use16
assume cs:code, ds:data, ss:stk
main:
   mov ax, data
   mov ds, ax
   ;Bochs config: port_e9_hack: enabled=1
   ;to insert a breakpoint here
   mov dx, 8A00h
   mov ax, 8A00h
   out dx, ax
   mov ax, 8AE0h
   out dx, ax
   ;
   call get_a20_status
   mov old_a20_status, al
   ;
   mov ah, 0DFh; A20 gated on
   call gate_a20
   jnz error
   jmp enable_a20_ok
back_real:
   mov ax, data
   mov ds, ax
   mov es, ax
   mov ah, old_a20_status
   call gate_a20; recover A20 status
   mov dx, offset succ_msg
   jmp exit
error:
   mov dx, offset fail_msg
exit:
   mov ah, 9
   int 21h
   mov ah, 4Ch
   int 21h

;output:
;  AL=A20 status
get_a20_status proc
   call test_8042
   mov al, 0ADh; disable keyboard
   out 64h, al
   call test_8042
   mov al, 0D0h; query status
   out 64h, al
   call test_8042
   in  al, 60h; AL=current A20 status
   push ax
   call test_8042
   mov al, 0AEh; enable keyboard
   out 64h, al
   call test_8042
   pop ax
   ret
get_a20_status endp

enable_a20_ok:
   mov dx, data; DX = real mode segment address
   mov ebx, data_len - 1; EBX= limit
   mov al, 93h ; AL = access 1001 0011
   mov ah, 00h ; AH = granularity 0000 0000
   mov si, data_sele; SI = selector
   call far ptr fill_gdt_item
   ;
   mov dx, pseg
   mov ebx, pseg_len - 1
   mov al, 9Bh ; code(eXecutable | Readable | Accessed)
   mov ah, 40h ; 32-bit
   mov si, pseg_sele
   call far ptr fill_gdt_item
   ;
   mov dx, tran_seg
   mov ebx, 10000h-1; limit must be compatible with real mode
   mov al, 9Bh ; code(eXecutable | Readable | Accessed)
   mov ah, 00h ; 16-bit
   mov si, tran_sele
   call far ptr fill_gdt_item
   ;
   mov dx, 0A800h
   mov ebx, 100000h - 1; 1M-1
   mov al, 93h; data(Writable | Accessed)
   mov ah, 40h; 32-bit flag(Bit6) is set for data segment whose limit exceeds 64K
   mov si, vram_sele
   call far ptr fill_gdt_item
   ;
   mov dx, code
   mov ebx, offset end_of_code - 1
   mov al, 9Bh; code(eXecutable | Readable | Accessed)
   mov ah, 00h; 16-bit
   mov si, code_sele
   call far ptr fill_gdt_item
   ;
   mov gdtr._gdtr_lim, gdt_len - 1
   mov dx, seg gdt
   mov ax, offset gdt
   movzx edx, dx
   movzx eax, ax
   shl edx, 4
   add edx, eax; edx = physical address of gdt
   mov gdtr._gdtr_bas_0_15, dx
   shr edx, 10h
   mov gdtr._gdtr_bas_16_31, dx
   lgdt fword ptr gdtr; gdtr是一个48位的变量
				   ;32位保护模式下的远指针变量也用fword ptr修饰，其中前32位为偏移地址
				   ;后16位为段地址
  				   ; 把gdt表的首地址及长度载入到gdtr寄存器
                   ; sgdt指令用来把gdtr的值保存到某个48位变量中
   cli
   mov eax, cr0
   and eax, 7FFFFFFEh; remove paging & protecting flag
   or eax, 1         ; enable protected mode flag
   mov cr0, eax      ; switch to protected mode
   ;在386以下CPU中(如80286), 上面4条指令的效果也可以用以下4条指令实现
   ;smsw ax          ; save machine status word
   ;and ax, 1Fh
   ;or al, 1
   ;lmsw ax          ; enable PE bit of MSW or CR0

   ;jmp far ptr 20h:0  ; 这里不能用常数表示远跳的段地址, 否则会有编译错误
   db 0EAh; jmp far ptr; 故用机器码来实现jmp far指令
   dw 0000h            ; protect's offset
   dw pseg_sele        ; protect's selector
                       ; jmp pseg_sele:protect
;-----------------------------------
;input:
;   DX = real mode segment address
;   EBX= limit
;   AL = access
;   AH = granularity
;   SI = selector
;output:
;gdt+si -> gdt item with info
fill_gdt_item proc far
    mov gdt[si].access, al
    mov gdt[si].gran, ah
    mov gdt[si].lim_0_15, bx; set lower 16 bits of limit
    shr ebx, 10h
    or  gdt[si].gran, bl; set higher 4 bits of limit
    movzx edx, dx
    shl edx, 4; convert segment address to physical address
    mov gdt[si].bas_0_15, dx ; set lower 16 bits of base address
    shr edx, 10h
    mov gdt[si].bas_16_23, dl; set mid 8 bits of base address
    mov gdt[si].bas_24_31, dh; set higher 8 bits of base address
    retf
fill_gdt_item endp

;---------------------------------------------------------------
; GATE_A20:  This routine controls a signal which gates address
;            line 20 (A20).  The gate A20 signal is an output of
;            of the 8042 slave processor (keyboard controller).
;            A20 should be gated on before entering protected
;            mode, to allow addressing of the entire 16M address
;            space of the 80286, or 4G address space of the
;            80386 & 80486.  It should be gated off after
;            entering real mode -- from protected mode.
;---------------------------------------------------------------
; Input:   AH = DD ==> A20 gated off (A20 always 0)
;          AH = DF ==> A20 gated on  (CPU controls A20)
; Output:  AL = 0  ==> Operation successful
;          AL = 2  ==> Operation failed, 8042 can't accept cmd
; Register(s) modified:  AX
;---------------------------------------------------------------
gate_a20 proc
;---------------------------------------------------------------
   cli             ; disable ints while using 8042
   call test_8042  ; insure 8042 input buffer empty
   jnz  a20_fail   ; ret: 8042 unable to accept cmd
   mov  al, 0ADh   ; disable keyboard
   out  64h, al 
   call test_8042
   jnz  a20_fail
   mov  al, 0D1h   ; 8042 cmd to write output port
   out  64h, al    ; output cmd to 8042
   call test_8042  ; wait for 8042 to accept cmd
   jnz  a20_fail   ; ret: 8042 unable to accept cmd
   mov  al, ah     ; 8042 port data
   out  60h, al    ; output port data to 8042
   call test_8042  ; wait for 8042 to port data
   jnz  a20_fail
   mov  al, 0AEh   ; enable keyboard
   out  64h, al
   call test_8042
a20_fail:
   sti
   ret
gate_a20 endp

;---------------------------------------------------------------
; test_8042:  This routine waits for the 8042 buffer to empty.
;---------------------------------------------------------------
; Input:   None
; Output:  AL = 0, 8042 input buffer empty:     ZF
;          AL = 2, Time out; 8042 buffer full:  NZ
; Register(s) modified:  AX
;---------------------------------------------------------------
test_8042 proc
   push cx
   xor cx, cx
test_again:
   in al, 64h; 8042 status port 
   jmp $+2   ; may be replaced with the instruction for delay: out 0EDh, ax
   test al, 2; input buffer full flag (Bit1==1)
   loopnz test_again
   jz test_8042_ret
test_next:
   in al, 64h
   jmp $+2
   test al, 2
   loopnz test_next
test_8042_ret:
   pop cx
   ret
test_8042 endp
end_of_code label byte
code ends
end main
```

cs、ds、es、ss、fs、gs这些段寄存器各有一个影子寄存器(shadow register),影子寄存器中保存了各个段的base、limit、 access, 只要我们不对段寄存器作赋值，cpu会使用影子寄存器中的值对内存进行访问(无视段寄存器的当前值)。

要是从32位的保护模式段直接跳回实模式段，会发生异常，因为cs的limit并没有修改成适合实模式的对应值，例如limit必须等于FFFF，同理ds、es的limit也要改成FFFF并且它们的权限要改成可读可写。

```
当前进程若访问了一个
无权访问的段就会产生一个
异常(General Protection Fault)
导致cpu调用int 0Dh中断, 此时若int 0Dh
没有对应的中断向量就会导致第2个异常(
double fault)使得cpu调用int 08h中断,
此时若int 08h中断向量也没有定义就会产
生第3个异常(triple fault),这时cpu会产
生一个reset信号使得计算机重启。

lgdt fword ptr gdtr; fword ptr表示48位宽度。
                   ; 32位保护模式下的远指针变量
                   ; 也用fword ptr修饰, 其中
                   ; 前32位为偏移地址, 后16位为
                   ; 段地址。

cs、ds、es、ss、fs、gs这些段寄存器各有一个
影子寄存器(shadow register), 影子寄存器中保存
了各个段的base、limit、access, 只要我们不对
段寄存器作赋值, cpu会使用影子寄存器中的值对
内存进行访问(无视段寄存器的当前值)。

要是从32位的保护模式段直接跳回实模式段,会发生
异常,因为cs的limit并没有修改成适合实模式的对应
值,例如limit必须等于FFFF,同理ds、es的limit也要
改成FFFF。

假定当前cs.cpl=3, 要调用的目标函数的段的dpl=0,
则必须要经过一个调用门(call gate)才可以, 否则直
接调用会发生GPF; 当目标函数的段的dpl=3时, 既可以
直接调用也可以通过调用门调用。假定当前cs.dpl=0,
则它在任何情况下(直接调用或通过调用门)都不可以
调用DPL=3的目标函数。
在目标段为数据段的情况下, cs.cpl <= 目标段.dpl;
当目标段为代码段的情况下, cs.cpl >= 目标段.dpl;
当目标段为代码段的情况下, cs.cpl > 目标段.dpl时
必须要通过调用门才能调用目标函数, 其中
cs.cpl <= CallGate.dpl;

调用门的描述符定义在gdt表中,格式如下:
gdt+0 ...
                +----目标函数的段地址即selector
                |        +----参数个数
                |        |    +----访问权限
                |        |    |
                =======  ==   ==
gdt+8 78h, 56h, 10h,00h, 02h, ECh, 34h, 12h
      --------                     --------
      |                                |
      |                                |
      +----目标函数的偏移地址----------+

访问权限ECh展开成二进制为:
1 1 1 0  1 1 0 0
- === -  =======
| |   |  |  
| |   |  +-------0Ch表示32位调用门
| |   +---系统描述符
| +---DPL=11
+---P;P=1表示存在

假定用户代码的cs.cpl=3, CallGate.dpl=3, 
CallGate->目标段.dpl=0, 则用户代码可以call目标
段内的函数。目标函数如何阻止用户代码利用传给目
标函数的指针让目标函数去访问用户代码无权访问的
内存?
char c;
c = f(0, 30h); 函数f()的功能是返回30h:0指向的字节
             ; 假定30h这个段的dpl=0
here:
当调用函数f()时, cpu做以下步骤:
mov ss, tss.ss0 ;\从用户堆栈切换到ring0的
mov esp, tss.esp;/堆栈空间;
                ; tss是当前任务状态段, tss的首地址在寄存器tr中,
                ; tss的8字节描述符在gdt表中;
push ss         ; ebp+18h
push esp        ; ebp+14
push 30h        ; ebp+10h
push 0          ; ebp+0Ch
push cs         ; ebp+8
push offset here; ebp+4
jmp far ptr f

f:              ; cs.cpl=0
push ebp
mov ebp, esp
mov ax, [ebp+10h]
mov cx, [ebp+8h]
arpl ax, cx; 当ax.rpl < cx.rpl时, ax.rpl=cx.rpl
           ; ax=33h
mov es, ax ; 对es赋值时会检查以下权限:
           ; ax.rpl <= 目标段(30h段).dpl &&
           ; cs.cpl <= 目标段(30h段).dpl
mov edi, [ebp+0Ch]
mov al, es:[edi]
pop ebp
retf

```

