# 1. 用堆栈传递参数有3种方式:

## (1) __cdecl 
参数从右到左顺序压入堆栈，由调用者清理堆栈;
是C语言参数传递规范。

__cdecl的例子:

```
f:
push bp; (4)
mov bp, sp
mov ax, [bp+4]; arg0
add ax, [bp+6]; arg1
pop bp; (5)
ret; (6)
main:
mov ax, 20; arg1
push ax; (1)
mov ax, 10; arg0
push ax; (2)
call f; (3)
here:
add sp, 4; (7)
```

__cdecl堆栈布局:

```
ss:1FF8 old bp <- bp (4)
ss:1FFA here <- (3)(5)
ss:1FFC 10 <- (2)(6)
ss:1FFE 20 <- (1)
ss:2000     <- (7)
```

## (2) __pascal
参数从左到右顺序压入堆栈，由被调用者清理堆栈;
是Pascal语言参数传递规范。
__pascal的例子：

```
f:
push bp; (4)
mov bp, sp
mov ax, [bp+6]; arg0
add ax, [bp+4]; arg1
pop bp; (5)
ret 4; (6)
main:
mov ax, 10
push ax; (1) arg0
mov ax, 20
push ax; (2) arg1
call f; (3)
here: 
```

__pascal的堆栈布局:

```
ss:1FF8 old bp<- bp (4)
ss:1FFA here <- (3)(5)
ss:1FFC 20 <- (2)
ss:1FFE 10 <- (1)
ss:2000    <-(6)
```

## (3) __stdcall
参数从右到左顺序压入堆栈，由被调用者清理堆栈;
是Windows API函数的参数传递规范。
__stdcall的例子：

```
f:
push bp; (4)
mov bp, sp
mov ax, [bp+4]; arg0
add ax, [bp+6]; arg1
pop bp; (5)
ret 4; (6)
main:
mov ax, 20
push ax; (1) arg1
mov ax, 10
push ax; (2) arg0
call f; (3)
here: 
```

__stdcall的堆栈布局:

```
ss:1FF8 old bp<- bp (4)
ss:1FFA here <- (3)(5)
ss:1FFC 10 <- (2)
ss:1FFE 20 <- (1)
ss:2000    <-(6)
```

32位__stdcall的例子:

```
f:
push ebp;(4)
mov ebp, esp
mov eax,[ebp+8];arg0
add eax,[ebp+0Ch];arg1
pop ebp;(5)
ret 8;(6)
main:
push 20;(1) arg1
push 10;(2) arg0
call f;(3)
here:
```

32位__stdcall的堆栈布局:

```
ss:1FF0 old ebp<- ebp (4)
ss:1FF4 here <- (3)(5)
ss:1FF8 10 <- (2)
ss:1FFC 20 <- (1)
ss:2000    <-(6)
```

# 2. 动态变量

```c
int f(int a, int b)
{
   int c; /* c是局部动态变量 */
   c = a+b;
   return c;
}
```

上述C语言函数可翻译成以下汇编代码:

```
f:
push bp; (4)
mov bp, sp
sub sp, 2; (5) 这里挖的坑就是给变量c的
mov ax, [bp+4]
add ax, [bp+6]
mov [bp-2], ax
mov ax, [bp-2]
mov sp, bp; (6)此时变量c死亡
pop bp; (7)
ret; (8)
main:
mov ax, 20
push ax; (1)
mov ax, 10
push ax; (2)
call f; (3)
here:
add sp, 4;(9)此时参数a,b死亡
```

执行上述代码时, 堆栈布局如下:

```
ss:1FF6 [30] (5) 变量c
ss:1FF8 old bp<- bp(4)(6)
ss:1FFA here <- (3)(7)
ss:1FFC 10 <- (2)(8)
ss:1FFE 20 <- (1)
ss:2000    <-(9)
```

# 3. C语言函数中需要保护bp, bx, si, di

C语言的函数里面除了不能破坏bp外，还要保护bx,si,di的值:

```
f:
push bp
mov bp, sp
sub sp, n; 其中n一个常数,用来为动态变量分配空间
push bx
push si
push di
...
pop di
pop si
pop bx
mov sp, bp
pop bp
ret
```

# 4. 递归

```c
int f(int n)
{
   if(n==1)
      return 1;
   return n+f(n-1);
}
```

上述C语言递归函数可翻译成以下汇编代码:

```
f:
push bp;(3)(6)(9)
mov bp, sp
mov ax, [bp+4]
cmp ax, 1
je done
dec ax
push ax;(4)(7)
call f;(5)(8)
there:
add sp, 2;(12)(15)
add ax, [bp+4]
done:
pop bp;(10)(13)(16)
ret;(11)(14)(17)
main:
mov ax, 3
push ax;(1)
call f;(2)
here:
add sp, 2;(18)
```

执行上述代码时的堆栈布局如下:

```
ss:1FEE oldbp<-bp(9)
ss:1FF0 there<-(8)(10)
ss:1FF2 1<-(7)(11)
ss:1FF4 oldbp<-bp(6)12
ss:1FF6 there<-(5)(13)
ss:1FF8 2<-(4)(14)
ss:1FFA oldbp<-bp(3)(15)
ss:1FFC here <-(2)(16)
ss:1FFE 3 <-(1)(17)
ss:2000   <-(18)
```

# 5. 混合语言编程

```
1. TC中内嵌汇编: asmtest.c
/*
编译步骤:
(1)把tasm.exe拷到tc文件夹内
(2)dos窗口输入以下命令编译:
   TCC asmtest.c
*/
main()
{
  int a=10, b=20, c;
  asm mov ax, a
  asm add ax, b
  asm mov c, ax
  printf("c=%d\n", c);
}


2. VC中内嵌汇编

#include "stdafx.h"
__declspec(naked) int f(int a, int b)
{
  __asm
  {
     push ebp
     mov ebp, esp
     mov eax, [ebp+8]
     add eax, [ebp+0x0C]
     mov esp, ebp
     pop ebp
     ret
  }   
}

int main()
{
  int y;
  y = f(10, 20);
  printf("%d\n", y);
  return 0;
}



3. 先用masm32编译called.asm, 再用VC调用其中的函数f()
3.1 called.asm

;编译步骤:
;(1)用qeditor打开called.asm
;(2)Project->Assemble ASM file
.386
.model flat, stdcall
option casemap :none
public f
.code
f:
   push ebp
   mov ebp, esp
   mov eax, [ebp+8]
   add eax, [ebp+0Ch]
   pop ebp
   ret
end


3.2 callasm.cpp

/* 编译步骤:
(1) 把masm32\called.obj拷到VC当前工程文件夹内
(2) Project->Add To Project->Files->文件类型选择Object Files->called.obj
(3) Build->Rebuild ALL
*/
#include "stdafx.h"
extern "C" int f(int a, int b); /* 此处声明函数f()是一个纯C函数，而非C++函数 */
/*
替换(2)的另外一个方案是: 
先用LIB命令把called.obj加到mylib.lib:  LIB /out:mylib.lib called.obj
再在此处插入以下语句,目的是告诉linker到mylib.lib中搜索源代码中找不到的函数的机器码
#pragma comment(lib, "mylib.lib")
*/
int main()
{
  int y;
  y = f(10, 20);
  printf("y=%d\n", y);
  return 0;
}

```

# 6. int、iret

```

int 21h对应的函数首地址保存在0:84h处, 该地址是一个远指针。
0:84 78h
0:85 56h
0:86 34h
0:87 12h
dword ptr 0:[84h]称为int 21h的中断向量(其实是它的函数首地址)
int n对应的中断向量的存储位置一定是0:n*4
n的取值范围是[00, FF], 所以256个中断向量会占用
0:0~0:3FF之间共400h个字节的内存，这块区域称为
中断向量表。
显然, int 00h的中断向量保存在dword ptr 0:[0];
int 01h的中断向量保存在dword ptr 0:[4];
int 03h的中断向量保存在dword ptr 0:[0Ch];
int 08h的中断向量保存在dword ptr 0:[20h];
int 0FFh的中断向量保存在dword ptr 0:[3FCh];
BIOS会完成部分中断向量的填写, 如int 10h、int 16h、int 13h这几个BIOS中断的向量在DOS启动前就已经填好了; DOS启动完成后，会填入int 21h的中断向量。

1000:2000 mov ah, 2
1000:2002 mov dl, 'A'
1000:2004 int 21h
1000:2006 mov ah,4Ch
上面这条int 21h指令执行时, cpu做了以下4件事:
pushf
push cs即1000h
push 下条指令的偏移地址即2006h
jmp dword ptr 0:[84h]; jmp 1234h:5678h

上面这条jmp会跳转到int 21h的中断服务函数内部:
1234:5678  ...
             ...
1234:56FF  iret; 中断返回
iret在执行时, cpu做了以下3件事情:
pop ip即ip=2006h
pop cs即cs=1000h
popf

1000:2000 call 1234:5678; 远调用
;此时会push 1000h 再 push 2005h
;最后jmp 1234:5678
1000:2005 mov ah, 4Ch
1000:2007 int 21h
...
1234:5678 ...
            retf; 此时执行pop ip再pop cs

中断程序例子:
http://10.71.45.100/bhh/int80.asm
http://10.71.45.100/bhh/int00.asm
http://10.71.45.100/bhh/int8.asm




```

# 7. 缓冲溢出
http://10.71.45.100/bhh/overflow.rar

# 8. 期末考试重点
http://10.71.45.100/bhh/asm_sum.doc