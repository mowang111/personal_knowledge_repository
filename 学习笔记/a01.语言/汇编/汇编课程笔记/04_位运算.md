# 算术运算

```
加、减、乘、除、求余
add 加
sub 减subtract
mul 乘multiply
div 除divide、求余
add 寄存器,寄存器 如 add ax, bx
add 寄存器,常数   如 add ax, 2
add 寄存器,变量   如 add ax, ds:[1000h]
add 变量, 寄存器  如 add ds:[1000h], ax
add 变量, 常数    如 add word ptr ds:[1000h], 2
注意不能add 变量, 变量

word ptr ds:[1000h]表示地址ds:1000h所指向的对象
是一个word类型的变量。
相当于C语言中的： *(short int *)(ds:1000h)

上述add指令可以换成sub或mov，语法是一样的。
汇编语言中常数没有宽度，例如1可以是8位也可以指16位也可以指32位。
word ptr相当于C语言中的short int *，其中ptr是pointer的缩写。
byte ptr相当于C语言中的char *
dword ptr相当于C语言中的long int *
add ax, bh 语法错误
mov bh, ax 语法错误
因为不等宽
```

# 逻辑运算

```

 &     |      ^      ~      <<     >>
and   or    xor    not    shl    shr

_rotl()    _rotr()
rol          ror

设AL=1011 0110
rol al, 1; 表示把AL循环左移1位
al = _rotl(al, 1); 表示把AL循环左移1位
结果AL=0110 1101

以下代码用<< | >>实现了循环左移运算:
unsigned long rol(unsigned long x, int n)
{
   return x << n | x >> (sizeof(x)*8-n);
}

shl: shift left
shr: shift right
xor: exclusive or
rol: rotate left 循环左移
ror: rotate right 循环右移

mov ah, 9Dh	; AH=1001 1101
rol ah, 1  	; AH=0011 1011
mov ah, 9Dh; AH=1001 1101
mov cl, 2
rol ah, cl	; AH=0111 0110

位运算的作用：
① 与运算可以使某些位变0；
设a是一个8位数，要使该数的最低位与最高位都变0，
其它位不变：
?011 011?
0111 1110  and)
0011 0110

② 或运算可以使某些位变1；
?011 011?
1000 0001 or)
1011 0111

③ 异或运算可以使某些位反转；
0011 0111           
1000 0001  xor)    
1011 0110
```

运用rol指令把16位整数转化成16进制格式输出:
http://10.71.45.100/bhh/v2h.asm

```
data segment
abc dw 32767
s db 4 dup(0),0Dh,0Ah,'$'
;把dos系统里的换行(0dh),回车（0ah）组成一个字符串，利用21h-09h输出字符串功能，在屏幕上输出换行的作法
;4 dup(0)相当于0,0,0,0
;s[0]='7'; s[1]='F'; s[2]='F'; s[3]='F'
data ends

code segment
assume cs:code, ds:data
main:
   mov ax, data
   mov ds, ax
   mov ax, abc
   mov cx, 4
   mov di, 0; 目标数组的下标,可以引用s[di]
again:
   push cx
   mov cl, 4 ; 设ax的原值=7A9Dh
   rol ax, cl; AX=A9D7, 9D7A, D7A9, 7A9D
   push ax
   and ax, 0000000000001111B; 000Fh
   cmp ax, 10
   jb is_digit
is_alpha:
   sub al, 10
   add al, 'A'
   jmp finish_4bits
is_digit:
   add al, '0'
finish_4bits:
   mov s[di], al
   pop ax
   pop cx
   add di, 1
   sub cx, 1
   jnz again
   mov ah, 9
   mov dx, offset s
   int 21h
   mov ah, 4Ch
   int 21h
code ends
end main
```



运用rol指令把32位整数转化成16进制格式输出:
http://10.71.45.100/bhh/v2h32.asm

```
.386
data segment use16
abc dd 2147483647
s db 8 dup(0),0Dh,0Ah,'$'
data ends
code segment use16
assume cs:code, ds:data
main:
   mov ax, data
   mov ds, ax
   mov eax, abc
   mov cx, 8
   mov di, 0; 目标数组的下标,可以引用s[di]
again:
   rol eax, 4; 386以上cpu中, 移位次数大于1时也可
             ; 使用常数
   push eax  ; 设eax=12345678h
   and eax, 0Fh; eax=00000008h, al=08h
   cmp al, 10
   jb is_digit
is_alpha:
   sub al, 10
   add al, 'A'
   jmp finish_4bits
is_digit:
   add al, '0'
finish_4bits:
   mov s[di], al
   pop eax
   add di, 1
   sub cx, 1
   jnz again
   mov ah, 9
   mov dx, offset s
   int 21h
   mov ah, 4Ch
   int 21h
code ends
end main
```

